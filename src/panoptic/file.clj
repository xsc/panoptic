(ns ^{:doc "File Representation"
      :author "Yannick Scherer"}
  panoptic.file
  (:require [clj-time.core :as t]
            [me.raynes.fs :as fs]))

;; ## File Map
;;
;; A file is represented as a map with only one mandatory field, namely `:path` containing
;; a fully-qualified path to a file. Additional data, used by Panoptic, includes:
;;
;; - `:checked`: unix timestamp of time the file was last checked for changes
;; - `:modified`/`:created`/`:deleted`: flags indicating the checking result
;; - `:checksum`: checksum value generated by a Panoptic checker (this might
;;    be anything comparable using `=`).

(defn file
  "Create new File Map using the given Path."
  [path]
  (-> {}
    (assoc :path (fs/absolute-path path))))

;; ## Panoptic Data

(let [E (t/epoch)]
  (defn- update-timestamp
    "Update `:checked` timestamp in File Map."
    [f] 
    (assoc f :checked (t/in-secs (t/interval  E (t/now))))))

(defn set-file-deleted
  "Set `:deleted` data in File Map, update timestamp and checksum."
  [f]
  (-> f
    (dissoc :created :modified)
    (assoc :deleted true) 
    (dissoc :checksum)
    (update-timestamp)))

(defn set-file-created
  "Set `:created` data in File Map, update timestamp and checksum."
  [f checksum]
  (-> f
    (dissoc :deleted :modified)
    (assoc :created true)
    (assoc :checksum checksum)
    (update-timestamp)))

(defn set-file-modified
  "Set `:modified` data in File Map, update timestamp and checksum."
  [f checksum]
  (-> f
    (dissoc :created :deleted)
    (assoc :modified true)
    (assoc :checksum checksum)
    (update-timestamp)))

(defn set-file-untouched
  "Clear all `:deleted`, `:modified` and `:created` data in File Map, update timestamp."
  [f]
  (-> f
    (dissoc :created :deleted :modified)
    (update-timestamp)))

;; ## Directory Map

(defn directory
  "Create directory map from directory path and additional options."
  [path & {:keys [extensions include-hidden]}] 
  (let [f (fs/file path)
        valid-extension? (if-not (seq extensions)
                           (constantly true)
                           (let [ext (set (map #(str "." (if (keyword? %) (name %) (str %))) extensions))]
                             #(contains? ext (fs/extension %))))]
    (when (and (fs/exists? f) (fs/directory? f))
      (let [path (fs/absolute-path f)
            children (fs/list-dir f)]
      (-> {}
        (assoc :path path)
        (assoc :include-hidden include-hidden)
        (assoc :extensions (set extensions))
        (assoc :files (->> children
                        (filter #(fs/file? (str path "/" %)))
                        (filter valid-extension?)))
        (assoc :directories (->> children
                              (filter #(fs/directory? (str path "/" %)))
                              (filter #(or include-hidden (not (.startsWith ^String % ".")))))))))))
